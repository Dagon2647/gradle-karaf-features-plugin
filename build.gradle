// *****************************************************************************
// 
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.2'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'com.bmuschko.nexus'

    version      = '1.0.0.SNAPSHOT'
    group        = 'com.github.lburgazzoli'

    ext {
        gradleVersion    = '2.2.1'
        gradleScriptDir  = "${rootProject.projectDir}/gradle"
        isReleaseVersion = !version.endsWith("SNAPSHOT")
        gitRoot          = "https://github.com/lburgazzoli"
        gitProject       = "https://github.com/lburgazzoli/lb-gradle-plugins"

        if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                nexusUsername = sonatypeUsername
                nexusPassword = sonatypePassword
            } else {
                nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
                nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
            }
        }
    }

    repositories {
        mavenCentral()    
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile gradleApi()
        compile localGroovy()

        testCompile "junit:junit:4.12"
    }

    jar {
        baseName = "${project.name}"

        manifest {
            attributes['Implementation-Title'   ] = "${group}.${project.name}-${project.version}"
            attributes['Implementation-Version' ] = project.version
            attributes['Implementation-Vendor'  ] = 'lb'
        }
    }

    // *************************************************************************
    //
    // *************************************************************************
    
    nexus {
        sign    = false
    }

    extraArchive {
        javadoc = true
        sources = true
        tests   = false
    }

    modifyPom {
        project {
            name          = "${project.name}"
            description   = "${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}
