// *****************************************************************************
// 
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

// *****************************************************************************
//
// *****************************************************************************

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'io.codearte.nexus-staging'

group               = 'com.github.lburgazzoli'
description         = 'A gradle plugin to create Karaf features'
sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

ext {
    gradleVersion    = '2.9'
    gradleScriptDir  = "${rootProject.projectDir}/gradle"
    gradlePluginId   = "com.github.lburgazzoli.karaf.features"
    gradlePluginTags = [ 'camel' ]
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    gitRoot          = "https://github.com/lburgazzoli"
    gitProject       = "https://github.com/lburgazzoli/gradle-karaf-features-plugin"

    if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }

    versions = [
        spock : '1.0-groovy-2.4'
    ]
}

repositories {
    mavenCentral()    
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:$versions.spock") {
        exclude(module: 'groovy-all')
    }
}

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Implementation-Title'   ] = "${group}.${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'
    }
}

// *****************************************************************************
// PUBLISHING
// *****************************************************************************

if(project.hasProperty('publishToJBoss')) {
    apply from: "$rootDir/gradle/publish-maven-jboss.gradle"
}

nexus {
    sign = project.isReleaseVersion
}

extraArchive {
    javadoc = true
    sources = true
    tests   = false
}

modifyPom {
    project {
        name          = project.name
        description   = project.name
        url           = project.gitProject
        packaging     = 'jar'

        licenses {
            license {
                name         = "The Apache Software License, Version 2.0"
                url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }

        scm {
            url                 = project.gitProject
            connection          = "scm:git:${project.gitProject}.git"
            developerConnection = "scm:git:${project.gitProject}.git"
        }
        
        developers {
            developer {
                id 'lburgazzoli'
                name 'Luca Burgazzoli'
                url gitRoot
            }
            developer {
                id 'sebersole'
                name 'Steve Ebersole'
            }
            developer {
                id 'SOVALINUX'
                name 'Sergey Nekhviadovich'
            }
        }
    }
}

pluginBundle {
    website     = project.gitProject
    vcsUrl      = project.gitProject
    description = project.name
    tags        = gradlePluginTags

    plugins {
        karafFeaturesPlugin {
            id          = gradlePluginId
            displayName = project.description
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn uploadArchives, publishPlugins
//closeAndPromoteRepository.dependsOn uploadArchives

task promote(dependsOn: closeAndPromoteRepository) {
    println "Release"
}
